buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'http://maven.epoxide.xyz'
    }
    maven {
        url 'http://dogforce-games.com/maven'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.10.99"
group= "gigaherz.survivalist" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Survivalist-1.12.0"

minecraft {
    version = "1.12-14.21.0.2382"
    runDir = "run"

    mappings = "snapshot_20170626"

    replace "@VERSION@", project.version
    replaceIn "Survivalist.java"
}

configurations {
    // configuration that holds jars to embed inside the jar
    embed
}

dependencies {
    deobfCompile "gigaherz.commons:Commons-1.12.0:0.6.1"
    embed "gigaherz.commons:Commons-1.12.0:0.6.1"

    deobfCompile "mezz.jei:jei_1.12:4.7.0.67:api"
    runtime "mezz.jei:jei_1.12:4.7.0.67"

    compile fileTree(dir: 'libs', include: '*.jar')
}

jar {
    into('/') {
        from configurations.embed
    }

    manifest {
        attributes(
                "ContainedDeps": configurations.embed.collect { it.getName() }.join(' ')
        )
    }
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
